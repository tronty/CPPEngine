//--------------------------------------------------------------------------------------
// File: HorizontalButterfly.shd
// 
// Copyright (c) ATI Research 2006
//--------------------------------------------------------------------------------------

[Fragment shader]

<compile ps_2_0 main>

float fPassNum;     // fPassNum corresponding row in butterfly map

sampler sampButterfly;
sampler sampSourceImage;

//--------------------------------------------------------------------------------------
// Pixel shader for horizontal butterfly pass
//--------------------------------------------------------------------------------------
float4 main( float2 srcLocation : TEXCOORD0 ) : COLOR
{
    float2 sampleCoord;
    float4 butterflyVal;
    float2 a, b, w;
    float temp;

    sampleCoord.x = srcLocation.x;
    sampleCoord.y = fPassNum;

    butterflyVal = tex2D(sampButterfly, sampleCoord);
    w = butterflyVal.ba;

    // Sample location A
    sampleCoord.x = butterflyVal.y;
    sampleCoord.y = srcLocation.y;
    a = tex2D(sampSourceImage, sampleCoord).ra;

    // Sample location B
    sampleCoord.x = abs(butterflyVal.x);
    sampleCoord.y = srcLocation.y;
    b = tex2D(sampSourceImage, sampleCoord).ra;

    // Multiply w*b (complex numbers) 
    temp = w.x*b.x - w.y*b.y;
    b.y = w.y*b.x + w.x*b.y;
    b.x = temp;

    // Perform a + w*b or a - w*b
    a = a + ((butterflyVal.x < 0.0) ? -b : b);

    // Make it a 4 component output for good measure
    return a.xxxy;
}
