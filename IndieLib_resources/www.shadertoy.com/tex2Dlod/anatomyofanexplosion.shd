/*
  Copyright (c) 2019 Tommi Roenty   http://www.tommironty.fi/
  Licensed under The GNU Lesser General Public License, version 2.1:
      http://opensource.org/licenses/LGPL-2.1
*/

struct VsIn {
    float3 position	: POSITION;
    float3 Normal	: NORMAL;
    float3 Binormal	: BINORMAL;
    float3 Tangent	: TANGENT;
    float3 Color	: TEXCOORD0;
    float2 uv		: TEXCOORD1;
};
struct VsOut {
    float4 position	: POSITION;
    float2 uv		: TEXCOORD0;
};

[Vertex shader]
ROW_MAJOR float4x4 worldViewProj MVPSEMANTIC;
VsOut main(VsIn In)
{
	VsOut Out=(VsOut)0;
	Out.position = mul(worldViewProj, float4(In.position, 1.0));
	Out.uv = In.uv;
	return Out;
}

[Fragment shader]
float3      iResolution;                  // viewport resolution (in pixels)
float     iTime;                        // shader playback time (in seconds)
float4      iMouse;                       // mouse pixel coords
sampler2D iChannel0;
sampler2D iChannel1;
float3      iChannelResolution[4];        // channel resolution (in pixels)

// change this to get different explosions :)
#define EXPLOSION_SEED 0.

// uncomment this to get a cross section view
//#define CROSS_SECTION

// the bounding sphere of the explosion. this is less general but means that
// ray cast is only performed for nearby pixels, and raycast can begin from the sphere
// (instead of walking out from the camera)
float expRadius;
float3 expCenter;

//iq's LUT 3D noise_
float noise_( in float3 x )
{
    float3 f = fract(x);
    float3 p = x - f; // this avoids the floor() but doesnt affect performance for me.
    f = f*f*(3.0-2.0*f);
     
    float2 uv = (p.xy+float2(37.0,17.0)*p.z) + f.xy;
    float2 rg = tex2Dlod( iChannel0, (uv+ 0.5)/256.0, 0.0 ).yx;
    return mix( rg.x, rg.y, f.z );
}

// assign colour to the media
float3 computeColour( float density, float radius )
{
	// these are almost identical to the values used by iq
	
	// colour based on density alone. gives impression of occlusion within
	// the media
	float3 result = mix( 1.1*float3(1.0,0.9,0.8), float3(0.4,0.15,0.1), density );
	
	// colour added for explosion
	float3 colBottom = 3.1*float3(1.0,0.5,0.05);
	float3 colTop = 2.*float3(0.48,0.53,0.5);
	result *= mix( colBottom, colTop, min( (radius+.5)/1.7, 1.0 ) );
	
	return result;
}

// maps 3d position to colour and density
float densityFn( in float3 p, in float r, out float rawDens, in float rayAlpha )
{
	// density has dependency on mouse y coordinate (linear radial ramp)
	float mouseIn = 0.85;
	if( iMouse.z > 0.0 )
		mouseIn = iMouse.y/iResolution.y;
	float mouseY = 1.0 - mouseIn;
    float den = -0.1 - 1.5*r*(4.*mouseY+.5);
    
	// offset noise_ based on seed
    float t = EXPLOSION_SEED;
    float3 dir = float3(0.,1.,0.);
    
    // participating media    
    float f;
    float3 q = p - dir* t; f  = 0.50000*noise_( q );
	q = q*2.02 - dir* t; f += 0.25000*noise_( q );
	q = q*2.03 - dir* t; f += 0.12500*noise_( q );
	q = q*2.01 - dir* t; f += 0.06250*noise_( q );
	q = q*2.02 - dir* t; f += 0.03125*noise_( q );
	
	// add in noise_ with scale factor
	rawDens = den + 4.0*f;
	
    den = clamp( rawDens, 0.0, 1.0 );
    
	// thin out the volume at the far extends of the bounding sphere to avoid
	// clipping with the bounding sphere
	den *= 1.-smoothstep(0.8,1.,r/expRadius);
	
	#ifdef CROSS_SECTION
	den *= smoothstep(.0,.1,-p.x);
	#endif
	
	return den;
}

float4 raymarch( in float3 rayo, in float3 rayd, in float expInter, in float2 fragCoord )
{
    float4 sum = float4( 0.0 );
     
    float step = 0.075;
     
    // dither start pos to break up aliasing
	float3 pos = rayo + rayd * (expInter + step*tex2D( iChannel0, fragCoord.xy/iChannelResolution[0].x ).x);
	
    for( int i=0; i<25; i++ )
    {
        if( sum.a > 0.99 ) continue;
		
		float radiusFromExpCenter = length(pos - expCenter);
		
		if( radiusFromExpCenter > expRadius+0.01 ) continue;
		
		float dens, rawDens;
		
        dens = densityFn( pos, radiusFromExpCenter, rawDens, sum.a );
		
		float4 col = float4( computeColour(dens,radiusFromExpCenter), dens );
		
		// uniform scale density
		col.a *= 0.6;
		
		// colour by alpha
		col.rgb *= col.a;
		
		// alpha blend in contribution
		sum = sum + col*(1.0 - sum.a);  
		
		// take larger steps through negative densities.
		// something like using the density function as a SDF.
		float stepMult = 1. + 2.5*(1.-clamp(rawDens+1.,0.,1.));
		
		// step along ray
		pos += rayd * step * stepMult;
    }
	
    return clamp( sum, 0.0, 1.0 );
}

// iq's sphere intersection
float iSphere(in float3 ro, in float3 rd, in float4 sph)
{
	//sphere at origin has equation |xyz| = r
	//sp |xyz|^2 = r^2.
	//Since |xyz| = ro + t*rd (where t is the parameter to move along the ray),
	//we have ro^2 + 2*ro*rd*t + t^2 - r2. This is a quadratic equation, so:
	float3 oc = ro - sph.xyz; //distance ray origin - sphere center
	
	float b = dot(oc, rd);
	float c = dot(oc, oc) - sph.w * sph.w; //sph.w is radius
	float h = b*b - c; // delta
	if(h < 0.0) 
		return -1.0;
	float t = (-b - sqrt(h)); //Again a = 1.

	return t;
}

float3 computePixelRay( in float2 p, out float3 cameraPos )
{
    // camera orbits around explosion
	
    float camRadius = 3.8;
	// use mouse x coord
	float a = iTime*20.;
	if( iMouse.z > 0. )
		a = iMouse.x;
	float theta = -(a-iResolution.x)/80.;
    float xoff = camRadius * cos(theta);
    float zoff = camRadius * sin(theta);
    cameraPos = float3(xoff,expCenter.y,zoff);
     
    // camera target
    float3 target = float3(0.,expCenter.y,0.);
     
    // camera frame
    float3 fo = normalize(target-cameraPos);
    float3 ri = normalize(float3(fo.z, 0., -fo.x ));
    float3 up = normalize(cross(fo,ri));
     
    // multiplier to emulate a fov control
    float fov = .5;
	
    // ray direction
    float3 rayDir = normalize(fo + fov*p.x*ri + fov*p.y*up);
	
	return rayDir;
}


float4 main(VsOut IN): COLOR 
{
	float2 fragCoord=IN.uv;
	// get aspect corrected normalized pixel coordinate
    float2 q = fragCoord.xy / iResolution.xy;
    float2 p = -1.0 + 2.0*q;
    p.x *= iResolution.x / iResolution.y;
    
    expRadius = 1.75;
	expCenter = float3(0.,expRadius,0.);
	
	float3 rayDir, cameraPos;
    rayDir = computePixelRay( p, cameraPos );
	
	float4 col = float4(0.);
	
    // does pixel ray intersect with exp bounding sphere?
	float boundingSphereInter = iSphere( cameraPos, rayDir, float4(expCenter,expRadius) );
	if( boundingSphereInter > 0. )
	{
		// yes, cast ray
	    col = raymarch( cameraPos, rayDir, boundingSphereInter,fragCoord );
	}
	
    // smoothstep final color to add contrast
    return float4(col.xyz*col.xyz*(3.0-2.0*col.xyz), 1);
}

